---
jupytext:
  text_representation:
    extension: .md
    format_name: myst
    format_version: 0.13
    jupytext_version: 1.16.4
kernelspec:
  display_name: Julia 1.10.4
  language: julia
  name: julia-1.10
---

+++ {"slideshow": {"slide_type": "slide"}}

# Julia Foundations

> Computational Analysis of Social Complexity
>
> Fall 2024, Spencer Lyon

**Prerequisites**

- Laptop or personal computer with internet connection

**Outcomes**

- Understand the main benefits and features of Julia
- See how to define variables, functions, and types in Julia
- Install commonly used packages for Graphs, DataFrames, Plotting and more

**References**

- [Packages and Software Engineering](https://julia.quantecon.org/more_julia/index.html) section of QuantEcon julia lectures
- Julia [documentation](https://docs.julialang.org/en/v1/)
- Documentation for packages: [Graphs](https://juliagraphs.org/Graphs.jl/dev/), [DataFrames](https://dataframes.juliadata.org/stable/), [Plots](https://docs.juliaplots.org/stable/)

+++ {"slideshow": {"slide_type": "slide"}}

## What is Julia?

- Julia is a relatively new programming language (first public release in 2012, 1.0 release in 2018)
- General purpose, but specializes in numerical computation
- Leverages advanced compiler technology to generate very efficient code
- It can be as clear to read and write as Python, and as quick to evaluate as C!

+++ {"slideshow": {"slide_type": "slide"}}

## Core Types

- We'll start by learning about the core datatypes built in to Julia
- Along the way we'll pick up some of the key syntax elements
- We will move quickly, so some prior programming experience would be helpful

+++ {"slideshow": {"slide_type": "subslide"}}

### Numbers

- Let's start with numbers
- To work with a number, just type it!

```{code-cell}
42
```

+++ {"slideshow": {"slide_type": "subslide"}}

- We can also do basic arithmetic in the way you would expect

```{code-cell}
10 * 3
```

```{code-cell}
1 + 2
```

+++ {"slideshow": {"slide_type": "subslide"}}

- So far we've worked with integers (whole numbers)
- Julia can also work with numbers containing a decimal
- In Julia these are called floating point numbers

```{code-cell}
1.234 ^ 2.2
```

```{code-cell}
553.34 / 12.9
```

+++ {"slideshow": {"slide_type": "subslide"}}

- We can mix and match integers and floats

```{code-cell}
25 / 2.5
```

```{code-cell}
25 / 2  # dividing integers returns a float (notice the `.`)
```

- notice we used `#` to define a comment

+++ {"slideshow": {"slide_type": "slide"}}

## Text Data

- Not all data is numerical
- Some is textual
- To represent text in Julia we use a `String`
- To define a String we use quotation marks (`"`) as below

```{code-cell}
"My name is Spencer"
```

```{code-cell}
"1"  # an integer in a string
```

+++ {"slideshow": {"slide_type": "subslide"}}

- You cannot use single quotes for strings as in other languages (like Python or Javascript)
- Go ahead... try it by removing the `#` and excuting the cell below

```{code-cell}
# 'hello'
```

+++ {"slideshow": {"slide_type": "slide"}}

## Arrays

- When doing numerical work, we often need to deal with multiple pieces of data at the same time
- In Julia the default way of doing this is to use an array
- Arrays are defined with `[` and `]` as below

```{code-cell}
[1, 2, 3]  # a 3 element array 
```

```{code-cell}
[1 2 3]  # a 1x3 matrix
```

```{code-cell}
[1 2; 3 4]  # a 2x2 matrix
```

```{code-cell}
[1 2
 3 4]  # another way to write a 2x2 matrix
```

```{code-cell}
[1 "hello"; 2 "world"]  # a 2x2 matrix with int and string
```

+++ {"slideshow": {"slide_type": "subslide"}}

### Accessing array items

- We can use `[N]` to access the Nth element
- We can also use `[i:j]` to access items `i` through `j`
- Finally we can use `[[n1, n2]]` to access the n1th and n2th elements

```{code-cell}
[100, 101, 102, 103][2]
```

```{code-cell}
[100, 101, 102, 103][2:4]
```

```{code-cell}
[100, 101, 102, 103][[1, 3]]
```

- Note that unlike Python, Julia starts counting at 1

+++ {"slideshow": {"slide_type": "slide"}}

## Tuples

- There is another data type for holding "lists" of data called a tuple
- Tuples are create using parenthesis instead of square brackets as follows

```{code-cell}
(1, 2, 3) 
```

```{code-cell}
("hello", 5)
```

```{code-cell}
("hello", 5)[2]
```

- The main differences between tuples and arrays are
    1. Tuples are meant to hold immutable or non-changing data
    2. Tuples aren't usually meant for computation or linear algebra
   

+++ {"slideshow": {"slide_type": "subslide"}}

## Dictionary

- Very often in programming we want to be able to associate a key or name to a specific value
- One data type for doing that is a Dict
- Dicts are created with the somewhat inconvenient syntax `Dict(name => value, ...)` where the `...` means we can repeat the pattern multiple times
- They keys and values can be of any type

```{code-cell}
Dict("x" => 1, 2 => "y", ["w", "z"] => [1, 2, 3])
```

```{code-cell}
# use `[name]` to acces element with `name`
Dict("x" => 1, 2 => "y", ["w", "z"] => [1, 2, 3])[2]
```

- Dictionaries are often used for passing around groups of parameters
- We'll see examples later on

+++ {"slideshow": {"slide_type": "subslide"}}

## Named Tuples

- The final "collection" we'll talk about is the named tuple
- It is a hybrid between a tuple and a dictionary
- To create them we use the synax `(name = value, ...)`
- They names or keys need to be just names (not numbers or arrays). The values can be anything

```{code-cell}
(x = 1, y = 2, z="hello")
```

```{code-cell}
(x = 1, y = 2, z="hello").z # use `.name` to access item
```

- Named tuples are a newer feature of Julia
- They are often used for the same purpsoes as dictionaries because the syntax is much cleaner

+++ {"slideshow": {"slide_type": "subslide"}}

## Variables

- Often when programming, we need to refer to the same piece of data more than once
- To do this we use a variable
- Variables are defined using an `=`, as in `name = value`

```{code-cell}
x = 1
```

```{code-cell}
y = 42
```

```{code-cell}
x + y  # 'use' or 'refer to' x and y
```

```{code-cell}
m1 = [1 0; 0 1]
```

```{code-cell}
m2 = [1 2; 3 4]
```

```{code-cell}
m1 * m2  # matrix multiplication
```

```{code-cell}
m2 * m2  # again -- but with something besides identity matrix!
```

```{code-cell}
d = Dict("X" => 1, "Y" => 2)
```

```{code-cell}
d["X"]
```

+++ {"slideshow": {"slide_type": "slide"}}

## Functions

- Most Julia programs do more than basic arithmetic operations on data
- To apply an operation to a piece of data, we call a function
- To call a function we use the `function_name(data1, data2)`
- A very handy function is the `typeof` function

```{code-cell}
typeof(1)
```

```{code-cell}
typeof(2.0)
```

```{code-cell}
typeof([1,2,3])
```

```{code-cell}
typeof([1 2; 3 4.0])
```

+++ {"slideshow": {"slide_type": "subslide"}}

- Many standard operations are built in to Julia as functions

```{code-cell}
sum([1, 2, 3])  # compute sum of array of numbers
```

```{code-cell}
inv([1 2; 3 4])  # matrix inverse
```

```{code-cell}
size([1 2; 3 4])  # number of (rows, columns)  in matrix
```

```{code-cell}
length([1, 2, 3])  # number of elements in array
```

```{code-cell}
length([1 2; 3 4])  # returns total number of elements in a Matrix
```

```{code-cell}
rand(2, 2, 2)  # a 2x2x2 array of random numbers, sampled from uniform[0,1] dist
```

+++ {"slideshow": {"slide_type": "subslide"}}

- Julia has 1000s of functions
- We'll learn more as we go along...
- Just watch for the pattern with parentisis: `name(args)`

+++ {"slideshow": {"slide_type": "slide"}}

### Defining Functions

- Functions are used to execute a predefined set of operations
- Defining our own funcitons allows us to break programs into small, easily written an understood components
- We define functions using the syntax
```julia
function name(arg1, arg2)
    # steps
end
```

```{code-cell}
function mean(x)
	total = sum(x)
	N = length(x)
	total / N
end
```

```{code-cell}
---
slideshow:
  slide_type: fragment
---
mean([1, 2, 3])
```

```{code-cell}
---
slideshow:
  slide_type: fragment
---
# mean of 1000 random samples from U[0,1] -- should be ~ 0.5
mean(rand(1000))  
```

+++ {"slideshow": {"slide_type": "subslide"}}

- If a function only contains one line of code, you can also use a shorthand notation:
```julia
function_name(arg1, arg2) = # step
```

```{code-cell}
add_two(x) = x + 2
```

```{code-cell}
add_two(40)
```

## Getting help for functions

- Given that there are so many functions, sometimes it is hard to remember exactly what a function does
- Thankfully we can get help from Julia
- If we type `?function_name`, Julia will present us with documentation about the function

```{code-cell}
?map
```

```{code-cell}
?extrema
```

## Control Flow

- Julia has the basic elements of control flow:
    - `if`-`else` statements
    - `for` loops
    

```{code-cell}
if 1 > 2 # no parenthesis needed
    println("what???")
else     # else is optional
    print("phew")
end      # all "blocks" terminate with word `end`
```

```{code-cell}
for i in 1:5 # range of numbers 1 to 5
    println(i, " ", i^2)
end
```

- We will see many more examples as we go forward

+++ {"slideshow": {"slide_type": "slide"}}

## Packages

- Julia comes ready to go with many powerful functions and data types
- However, there is a very active community of Julia programmers who are experts in different subfields of science and engineering
- This has led to the development of vibrant and exciting ecosystem of packages or toolboxes for performing specific tasks
- We can access these routines by using Julia packages

+++

### Loading packages

- By default Julia ships with a "standard library"
- These are packages that come bundled with Julia itself and are pre-installed
- To load a package and all of its types/functions use the `using` keyword
- For example, we can load the `Dates` package and start using it

```{code-cell}
using Dates
```

```{code-cell}
t1 = Dates.now()
```

```{code-cell}
Dates.format(t1, "yyyy-mm-dd")
```

```{code-cell}
t2 = Dates.now()
```

```{code-cell}
t2 > t1
```

```{code-cell}
t3 = DateTime(1776, 7, 4)
```

```{code-cell}
"America is $(t1 - t3) ($(floor(t1 - t3, Dates.Day))) old"
```

### Installing Packages

- In addition to the standard library, we can also use packages created by other Julia users
- To use a 3rd party package, we first need to install it
- There are two ways to do this

**1**

```julia
]add PackageName
```

**2**
```julia
using Pkg  # a standard library package
Pkg.add("PackageName")
```

Let's try them both

```{code-cell}
]add Plots
```

```{code-cell}
using Pkg
Pkg.add("DataFrames")
```

- After installing packages, we can load and use them just as we did the standard library packages

```{code-cell}
using Plots
```

```{code-cell}
plot([sin, cos], -2pi, 2pi)
```

```{code-cell}
using DataFrames
df = DataFrame(c1=1:10, c2=(1:10).^2)
```

### Package Composability

- One unique feature sof Julia is that most of the language itself, in addition to packages, are written in Julia
- For other languages like Python or R the "built in" part of the language is often written in another language like C
- This difference has a large impact for Julia users
    - Built in code and user code (including packages) are given the same "treatment"
    - Anything the language creators can do, so can you
- A practical implication of this is that packages can operate on built in types (like we saw in our examples above) as well as types from other packages
- Let's see what this looks like by plotting a DataFrame

```{code-cell}
# install "StatsPlots", which links Plots and DataFrames
Pkg.add("StatsPlots")

using StatsPlots
```

```{code-cell}
@df df scatter(:c1, :c2)
```

```{code-cell}
Pkg.add("RDatasets") # common datasets from R programming language
using RDatasets
school = RDatasets.dataset("mlmRev","Hsb82")
@df school density(:MAch, group = :Sx)
```

```{code-cell}
@df school density(:MAch, group = (:Sx, :Sector))
```

```{code-cell}

```
