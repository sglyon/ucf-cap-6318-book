---
jupytext:
  text_representation:
    extension: .md
    format_name: myst
    format_version: 0.13
    jupytext_version: 1.16.4
kernelspec:
  display_name: Julia 1.10.4
  language: julia
  name: julia-1.10
---

# Agent Based Models Review/Practice

> Computational Analysis of Social Complexity

**Prerequisites**

- ABM
- Shelling segregation model

**Outcomes**

- Recall key components of ABM
- Review additional example of ABM in Julia


**References**

- https://www.researchgate.net/publication/328774079_Mesa_An_Agent-Based_Modeling_Framework
- https://juliadynamics.github.io/Agents.jl/stable/
- https://juliadynamics.github.io/AgentsExampleZoo.jl/dev/examples/wealth_distribution/

+++

## Agent Based Models

- Agent based models are...
    - An approximation to some complex system (a model)
    - Used in various fields including biology, epidemiology, economics, etc.
    - Composed of three key elements: (1) Agents (2) Environment (3) Rules

+++

## Shelling Segregation Model

- Recall the Schelling segregation model...
- Agents: individuals/families seeking a home in a neighborhood. Have a type and hapiness. Agents are happy if least $N$ of their neighbors are same type
- Environment: grid of "lots" or homes where agents can live
- Rules: All unhappy agents move to a new random home in the environment

+++

### Shelling Takeaways

- Very simplistic view of agents (people) and decision making criterion (rules)
- Agents only considered immediate neighbors when deciding to move (locality)
- Simplistic, local behavior led to stark aggregate results: segregation of neighborhoods into agent types
- Agents.jl implementation very straightforward: `struct` to represent agent, `struct` (Agents.jl defined) to represent environment, `function` to represent rules for single agent

+++

## Plan today

- See example of second model
- Break into groups and study example models from the [Agents.jl model zoo](https://juliadynamics.github.io/AgentsExampleZoo.jl/dev/)
    - Present model your group studied

+++

## Money Model

- Agents:
    - $N$ Agents
    - All start with 1.0 wealth
- Environment: none -- they just exist ;)
- Rules:
    - If agent has at least 1.0 wealth, gives 1.0 wealth to another agent
    - If agent has 0 wealth, does nothing

+++

### Agents

```{code-cell}
# import Pkg
# Pkg.activate(".")
# Pkg.instantiate()
```

```{code-cell}
using Agents, Random, DataFrames

@agent struct MoneyAgent(NoSpaceAgent)
    wealth::Int
end
```

### Rules

```{code-cell}
function agent_step!(agent, model)
    if agent.wealth == 0
        return
    end
    recipient = random_agent(model)
    agent.wealth -= 1
    recipient.wealth += 1
end
```

### Model

```{code-cell}
function money_model(; N = 100)
    m = ABM(MoneyAgent; agent_step!)
    for _ in 1:N
        add_agent!(m, 1)
    end
    return m
end

money_model()
```

### Simulation

```{code-cell}
m = money_model(N=2000)
adata = [:wealth]
df, _ = run!(m, 10; adata)
```

```{code-cell}
using CairoMakie
```

```{code-cell}
hist(
    filter(x -> x.time == 10, df).wealth;
    bins = collect(0:9),
    color = cgrad(:viridis)[28:28:256],
)
```

- Above we see the famous "power law" pattern
- This is a common result in many areas of economics: that activity or wealth is concentrated in the very few (the 1%) 
    - References: https://pubs.aeaweb.org/doi/pdf/10.1257/jep.30.1.185, https://www.sciencedirect.com/science/article/abs/pii/S0378437197002173, https://www.theguardian.com/commentisfree/2011/nov/11/occupy-movement-wealth-power-law-distribution

+++

## Money Model with space

- Let's expand our environment to a 2d-grid and only allow sharing wealth amongst neighbors
- Changes relative to previous setup:
    - Agents now have a position
    - Environment is 2d `GridSpace`
    - Agents randomly pick a neighbor to give money to

```{code-cell}
@agent struct WealthInSpace(GridAgent{2})
    wealth::Int
end
```

```{code-cell}
function agent_step!(agent::WealthInSpace, model)
    if agent.wealth == 0
        return
    end
    
    recipient = rand(collect(nearby_agents(agent, model)))
    agent.wealth -= 1
    recipient.wealth += 1
end
```

```{code-cell}
function money_model_2d(; dims = (25, 25))
    space = GridSpace(dims, periodic = true)
    model = ABM(WealthInSpace, space; scheduler = Schedulers.Randomly(), agent_step! = agent_step!)

    fill_space!(model, 1)
    return model
end
```

```{code-cell}
m2d = money_model_2d()
adata2d = [:wealth, :pos]
df2d, _ = run!(m2d, 10; adata=adata2d)
```

```{code-cell}
hist(
    filter(x -> x.time == 10, df2d).wealth;
    bins = collect(0:9),
    color = cgrad(:viridis)[28:28:256],
)
```

- Still a power law... very pervasive!

```{code-cell}
function make_heatmap(model, df, T=maximum(df.step))
    df_T = filter(x -> x.time == T, df)
    
    x = combine(groupby(df_T, :pos), :wealth => sum)
    arr = zeros(Int, size(getfield(model, :space)))
    
    for r in eachrow(x)
        arr[r.pos...] += r.wealth_sum
    end
    
    figure = Figure(; size = (600, 450))
    hmap_l = figure[1, 1] = Axis(figure, title="T= $T")
    hmap = heatmap!(hmap_l, arr; colormap = cgrad(:default))
    cbar = figure[1, 2] = Colorbar(figure, hmap; width = 30)
    return figure
end
```

```{code-cell}
for t in 0:10
    display(make_heatmap(m2d, df2d, t))
end
```

```{code-cell}

```
