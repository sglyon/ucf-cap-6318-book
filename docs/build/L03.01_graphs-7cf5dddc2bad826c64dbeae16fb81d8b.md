---
jupytext:
  text_representation:
    extension: .md
    format_name: myst
    format_version: 0.13
    jupytext_version: 1.16.4
kernelspec:
  display_name: Julia 1.10.4
  language: julia
  name: julia-1.10
---

+++ {"slideshow": {"slide_type": "slide"}}

# Graphs

> Computational Analysis of Social Complexity
>
> Fall 2024, Spencer Lyon

**Prerequisites**

- Julia setup
- Julia basics
- Julia types and methods

**Outcomes**

- Understand key components of networks/graphs
- Use the Graphs.jl package for working with graphs in Julia
- Implement the breadth-first search algorithm

**References**

- [Easley and Kleinberg](https://www.cs.cornell.edu/home/kleinber/networks-book/) chapter 2

+++

## Introduction

+++ {"slideshow": {"slide_type": "slide"}}

### Why Study Graphs?
- Economic, cultural, political, and social interactions are influenced by *structure* of relationships
  - Transmission of viruses
  - International trade, supply chains, marketplaces
  - Spread of information, diffusion of innovation
  - Political persuasion, localized voting patterns
  - Human behaviors influenced by network of friends (sports, clothes, music)
- Behaviors can be effected by social networks
  - "Influencers"
  - Circles of followers can create echo chambers

+++ {"slideshow": {"slide_type": "slide"}}

### Edges and Nodes

- A **graph** specifies relationships between a collection of items
- Each item is called a **node**
- A relationship between nodes is represented by an **edge**
- Visually, graphs might look like this:

![Graphs](https://ucf-cap-6318-resources.s3.amazonaws.com/images/graph_structure.png)

+++

- Here the nodes are `A`, `B`, `C`, `D`
- The edges connect nodes `A-B`, `B-C`, `B-D`, `C-D`

+++ {"slideshow": {"slide_type": "subslide"}}

### Adjacency Matrix

- How might we represent the graph above numerically?
- One very common approach is to use a matrix of 0's and 1's called an *adjancency matrix*
- Suppose we have a graph of $N$ nodes
  - Without loss of generality, we'll represent them as integers `1:N`
- Let $A \in \{0,1\}^{N \times N}$ be our adjacency matrix
- Element $A_{ij}$ will be zero unless there is an edge between nodes $i$ and $j$ (diagonal is left as $0$)
- In our above we had
  - Nodes `A`, `B`, `C`, `D` (or 1, 2, 3, 4 respectively)
  - Edges connecting nodes `1-2`, `2-3`, `2-4`, `3-4`
- The adjacency matrix for this example is
$$A = \begin{bmatrix}
0 & 1 & 0 & 0 \\
1 & 0 & 1 & 1 \\
0 & 1 & 0 & 1 \\
0 & 1 & 1 & 0
\end{bmatrix}$$

+++ {"slideshow": {"slide_type": "slide"}}

## Graphs in Julia

- In Julia there are a few ways we could represent our example graph above
- We could start with the adjacency matrix concept as follows

```{code-cell}
A = [
	0 1 0 0
	1 0 1 1
	0 1 0 1
	0 1 1 0
]
```

+++ {"slideshow": {"slide_type": "subslide"}}

### Working with Adjacency Matrices

- An adjacency matrix gives us a lot of information about the structure of the graph
- We could compute all of the following
  - Total number of nodes: number of rows or columns of $A$
  - Total number of edges: $\sum_{ij} A_{ij}$
  - Node with most edges: $\text{argmax}_{i} \sum_{j} A_{i,j}$
  - Average number of edges per node: $N \cdot \sum_{i,j} A_{i,j}$

+++ {"slideshow": {"slide_type": "subslide"}}

### Exercise: Adjacency Matrix

- In the cell below we have defined an adjacency matrix called `A_ex1`
- Using `A_ex1` answer the following questions:
  - How many nodes are in the graph?
  - How many edges?
  - Node with most edges (hint, use the `dims` argument to `sum` and then the `argmax` function)
  - Average number of edges per node
  - Number of connections for node 7: $\sum_j A_{j7}$1

```{code-cell}
A_ex1 = rand(0:1, 30, 30)

# remove diagonal elements
for i in 1:30
    A_ex1[i, i] = 0
end
```

```{code-cell}
# Your code here

ex1_total_nodes = missing
ex1_total_edges = missing
ex1_node_most_edges = missing
ex1_average_edges_per_node = missing
ex1_connections_node_7 = missing
```

+++ {"slideshow": {"slide_type": "slide"}}

### Graphs.jl

- There are many smart graph theory experts in the Juila community
- They have built a package called Graphs for working with graphs (as well as ancillary pacakges for extra features)
- We can build a `Graphs.Graph` object directly from our adjacency matrix

```{code-cell}
using Graphs
```

```{code-cell}
G1 = Graph(A)
```

```{code-cell}
collect(edges(G1))  # collect turns an `iterator` into an array
```

```{code-cell}
collect(vertices(G1))  # Graphs refers to nodes as `vertices`
```

+++ {"slideshow": {"slide_type": "subslide"}}

#### Visualizing Graphs

- We can use the GraphPlot package to visualize our graph
- Note that the actual placement of the nodes is randomly generated and then tweaked to clearly show all nodes and edges
- The important thing is *not* the placement of nodes, but rather their *relative structure*

```{code-cell}
using GraphPlot  # load GraphPlot package
```

```{code-cell}
gplot(G1)
```

+++ {"slideshow": {"slide_type": "subslide"}}

### Size considerations

- Using `Array{Int64,2}` to store an adjacency matrix turns out to be a rather costly way to store a graph
- In the original example graph we had 4 nodes and 4 edges
- To store this we needed to have a 4x4 matrix of 64 bit integers
  - This is only (`Int(16 * 64 / 8) == 128`) bytes in our exapmle,
  - But consider a graph of websites and links between them -- that graph would have millions of nodes and edges...
- There are a few approaches to reducing this storage cost:
  - Only store the upper triangle of the matrix
  - Use `Array{Bool,2}` instead of `Array{Int64,2}` to store adjacency matrix ( each element only `sizeof(Bool) == 1` bit!)
  - Use a [SparseMatrix](https://docs.julialang.org/en/v1/stdlib/SparseArrays/)
  - Store as a `Vector{Vector{Int}}`

```{code-cell}
---
slideshow:
  slide_type: subslide
---
# Vector{Vector{Int}}
A2 = [[2], [1, 3, 4], [2, 4], [2, 3]]
```

```{code-cell}
G1.fadjlist
```

+++ {"slideshow": {"slide_type": "slide"}}

## Graph Theory Concepts

- Let's explore some concepts often used in analysis of graphs

+++ {"slideshow": {"slide_type": "subslide"}}

### Paths

- When studying graphs it is often natural to ask about how things travel or flow across the graph
- For example, how information spreads amongst a group of friends, how data travels the internet, how diseases are transmitted from one person to another, and how people navigate a metro subway system
- In each of these cases, the flow of things goes from node to node across edges
- A flow from one any node to another node is called a **path**

+++ {"slideshow": {"slide_type": "subslide"}}

### Arpanet Example

- Consider the following Graph of the first iteration of the internet

![arpanet](https://ucf-cap-6318-resources.s3.amazonaws.com/images/arpanet_map.png)

- There are many possible paths through this network
- Consider a path from UCSB to MIT: `UCSB-UCLA-RAND-BBN-MIT`
- Another possible path from UCSB to MIT is `UCSB-SRI-UTAH-MIT`

+++ {"slideshow": {"slide_type": "subslide"}}

### Graphs.jl Arpanet

- Let's define the Arpanet using Graphs as it will be helpful throughout this lecture

```{code-cell}
nodes = [
		"UCSB" => ["SRI", "UCLA"],
		"SRI" => ["UCSB", "UCLA", "STAN", "UTAH"],
		"UCLA" => ["SRI", "UCSB", "STAN", "RAND"],
		"STAN" => ["SRI", "UCLA"],
		"UTAH" => ["SRI", "SDC", "MIT"],
		"SDC" => ["UTAH", "RAND"],
		"RAND" => ["UCLA", "SDC", "BBN"],
		"MIT" => ["UTAH", "BBN", "LINC"],
		"BBN" => ["MIT", "RAND", "HARV"],
		"LINC" => ["MIT", "CASE"],
		"CASE" => ["LINC", "CARN"],
		"CARN" => ["CASE", "HARV"],
		"HARV" => ["CARN", "BBN"]
	]
node_ints = Dict(zip(first.(nodes), 1:length(nodes)))
arpa = SimpleGraph(length(nodes))
for (node, edges) in nodes
    for e in edges
        add_edge!(arpa, node_ints[node], node_ints[e])
    end
end

# save graph for loading in future
savegraph("arpanet.lg", arpa)
```

```{code-cell}
arpa
```

```{code-cell}
gplot(arpa, nodelabel=first.(nodes))
```

+++ {"slideshow": {"slide_type": "subslide"}}

### Cycles

- An important concept when analyzing graphs is the concept of a cycle
- A cycle is a path that starts and ends at the same node
- For the ARPA net, an example cycle is `LINC-CASE-CARN-HARV-BBN-MIT-LINC`
- Question... what is the shortest possible cycle in a graph (including all endpoints)?
- Graphs can tell us if a graph is connected

```{code-cell}
is_connected(arpa)
```

+++ {"slideshow": {"slide_type": "subslide"}}

- It is natural to believe that many real-world networks are connected
  - Transportation: you can get to any station
  - Internet: you can visit any website
- But it is entirely possible to have a non-connected graph
  - Social networks (nodes: people, edges: friendships) of college students who different countries
  - Suppliers for a textile company vs a microchip manufacturer

+++ {"slideshow": {"slide_type": "subslide"}}

### Distance

- We can extend concept of paths between nodes, to include a notion of distance
- The **length** of a path is the number of steps it takes from beginning to end
  - `MIT-BBN-RAND-UCLA` has length 3 (starting from `MIT` take three steps before ending at `UCLA`)
- The **distance** between two nodes, is the length of the *shortest* path between those nodes
- Graphs can compute distances using the `gdistances` function
- Below we compute the distance between `UCLA` and all nodes

```{code-cell}
Dict(zip(first.(nodes), gdistances(arpa, node_ints["UCLA"])))
```

+++ {"slideshow": {"slide_type": "slide"}}

## Breadth-First Search

- If asked, how would you go about computing the distance between the `HARV` node and all other nodes?
- One iterative approach might be:
  - Start with `HARV`: note it is distance zero to `HARV`
  - Move on to all nodes directly connected to `HARV`: these are distance 1
  - Then move to all nodes connected to nodes that are distance 1 from `HARV` (excluding any you may have already found): declare these to be at distance 2 from `HARV`
  - Continue traversing edges until you have visited all nodes
- This algorihtm is called **breadth-first search**

+++ {"slideshow": {"slide_type": "subslide"}}

### Example: Breadth-First Search from MIT

- The image below shows how breadth-first search would proceed for the MIT node

![mit bfs](https://ucf-cap-6318-resources.s3.amazonaws.com/images/mit_breadth_first.png)

+++ {"slideshow": {"slide_type": "subslide"}}

### Exercise (difficult!): BFS

- Now it is time for you to try this out!
- Our goal is to use breadth-first search to compute the distance betwen a given node and all other nodes
- The return value you end up with should be an `Vector{Vector{Int}}`, where element `i` of this vector contains all node labels at distance `i` from the starting node
- Fill in the logic for the `breadth_first_distances` function below

```{code-cell}
function breadth_first_distances(g, start::Int)
	out = Vector{Int}[]
	# use push!(out, new_nodes) to add to out
	distance = 0

	# TODO: your code here...

	# return out
	out
end
```

```{code-cell}
# Test code

function test_bfd_methods(val, want)
    if length(val) == 0
        error("Make sure to `push!` on to `out` in your function")
    elseif length(val) != maximum(gdistances(arpa, node_ints["HARV"]))
        error("`out` has incorrect number of elements")
    elseif length.(val) != length.(want)
        error("Right number of elements, but not right number in each subvector")
    elseif all(map(x12 -> all(sort(x12[1]) .== sort(x12[2])), zip(val, want)))
        println("correct!")
    end
end

function run_tests()
    val = breadth_first_distances(arpa, node_ints["HARV"])
    want = [[9, 12], [7, 8, 11], [3, 6, 5, 10], [1, 2, 4]]
    test_bfd_methods(val, want)
end
```

```{code-cell}
# uncomment the code below and run when you are ready to test your code
# run_tests()
```

+++ {"slideshow": {"slide_type": "subslide"}}

### BFS with Graphs

- The Graphs library contains routines implementing breadth-first search
- The main function is called `bfs_tree`

```{code-cell}
bfs_carn = bfs_tree(arpa, node_ints["CARN"])
```

- Notice that the printout says we have a graph with 13 nodes, 12 edges and it is a **directed** graph
- Thus far, all graphs we have considered have been undirected
  - We have only been concerned about if a connection (edge) exists between nodes
- A directed graph extends the notion of connecting nodes with a direction
  - We can now say that *things* flow across edges from one node to another -- always in the same direction
- Why would the breadth-first search routine return a directed graph instead of the undirected type we started with?
- Let's visualize it and see if we can understand why

```{code-cell}
---
slideshow:
  slide_type: subslide
---
gplot(bfs_carn, nodelabel=first.(nodes))
```

- Notice that arrows only flow *out* of `CARN`
- They also always flow *away* from `CARN`
- The use of directed edges allows Graphs to represent the shortest path from CARN to any other node
  - For example `STAN`: `CARN -> HARV -> BBN -> RAND -> UCLA -> STAN`

+++ {"slideshow": {"slide_type": "subslide"}}

### Exercise: Explore DiGraph

- The `bfs_carn` object has type $(Markdown.Code(string(typeof(bfs_carn))))
- Let's view the names of its properties (properties)

```{code-cell}
propertynames(bfs_carn)
```

```{code-cell}
bfs_carn.fadjlist
```

- The `fadjlist` (forward adjacency list) property is a `Vector{Vector{Int64}}`
- `fadjlist` has one element per node (call index into outer Vector `i` for node `i`)
- Each element is itself a vector containing node indices for all nodes `j` for which there is an edge flowing from `i` to `j`
-  Below we have set up a new **method** (see below) for the `breadth_first_distances` **function** that takes a `DiGraph` as an argument
- Your task is to implement the the method so that it has the same return value as the previous method from above

```{code-cell}
function breadth_first_distances(g::SimpleDiGraph, start::Int)
	out = Vector{Int}[]
	# use push!(out, new_nodes) to add to out
	distance = 0

	# TODO: your code here...

	# return out
	out
end
```

```{code-cell}
# test code
function test_digraph_ex()
	val = breadth_first_distances(
		bfs_tree(arpa, node_ints["HARV"]),
		node_ints["HARV"]
	)
	want = [[9, 12], [7, 8, 11], [3, 6, 5, 10], [1, 2, 4]]
	test_bfd_methods(val, want)
end
```

```{code-cell}
# uncomment the code below when you are ready to test your code!
# test_digraph_ex()
```

+++ {"slideshow": {"slide_type": "slide"}}

## Components

- A *component* of a graph is a self-contained subset of the nodes
- More precisely, a set of nodes is a component if
  1. Every node in the subset has a path to every other node in the subset
  2. The subset is not part of a larger set with property (1)
- Example:

![components](https://ucf-cap-6318-resources.s3.amazonaws.com/images/graph_components.png)

+++ {"slideshow": {"slide_type": "subslide"}}

## Example

- How many components are in this graph?

![hs dating](https://ucf-cap-6318-resources.s3.amazonaws.com/images/hs_dating_graph.png)
